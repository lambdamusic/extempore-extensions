;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;
;; helper functions for generatic chords, scales and other musical structures 
;;
;;
;; author: @lambdamusic
;;;;;;;;;;;;;;;;;;;;;;;;;;


;
;
; mkchord
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Args:
; root 
; chord-sym [default: '^]
; notes_number [default: 3]
;
; Example:
; (println (mkchord 60 '- 8))
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(define-macro (:mkchord . args)
  ;;(println args)
   (cond ((length-equal? args 1) ;; just root 
         `(pc:make-chord-fixed ,(car args) 3 (pc:chord ,(car args) '^)))
         ((length-equal? args 2) ; root and chord-sym
         `(pc:make-chord-fixed ,(car args) 3 (pc:chord ,(car args) ,(cadr args))))
         ((length-equal? args 3) ;; also notes_number
         `(pc:make-chord-fixed ,(car args) ,(caddr args) (pc:chord ,(car args) ,(cadr args))))         
         (else (print 'Error: 'arguments 'could 'not 'be 'resolved.))))





;
;
; :mkchordrandom
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; wrapper around non-deterministic pc:make-chord (updated on 2020-10-04)
;
; Args:
; root 
; chord-sym [default: '^]
; notes_number [default: 3]
; upper_limit [default: 120]
;
; Example:
; (println (mkchordrandom 60 '- 3 120))
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(define-macro (:mkchordrandom . args)
  ;;(println args)
   (cond ((length-equal? args 1) ;; just root 
         `(pc:make-chord ,(car args) 120 3 (pc:chord ,(car args) '^)))
         ((length-equal? args 2) ; root and chord-sym
         `(pc:make-chord ,(car args) 120 3 (pc:chord ,(car args) ,(cadr args))))
         ((length-equal? args 3) ;; also notes_number
         `(pc:make-chord ,(car args) 120 ,(caddr args) (pc:chord ,(car args) ,(cadr args))))  
         ((length-equal? args 4) ;; also upper-limit
         `(pc:make-chord ,(car args) ,(cadddr args) ,(caddr args) (pc:chord ,(car args) ,(cadr args))))         
         (else (print 'Error: 'arguments 'could 'not 'be 'resolved.))))






;
;
; helper:multiply_octaves
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; helper function: from a list, extend it by adding 12 to all elements (one octave)
;
; Args:
;
; Example:
; (helper:multiply_octaves (pc:scale 0 'ionian) 2)) 
;; => retuns 2 octaves of the scale
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(define helper:multiply_octaves 
  (lambda (scale octaves)
    (if (= octaves 1)
      scale)
      (dotimes (i octaves)
        (if (> i 0)
          (set! scale (append scale (add (* 12 i) scale)))))
          scale))





;
;
; :mkscale
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Args:
; root    [default: 60]
; mode    [default: 'ionian]
; num-notes [default: 1 octave / 7 notes]
; octaves [default: 1]
;
;
; Example:
; (:mkscale 60) ;=> 7 notes, ionian
; (:mkscale 60 'aeolian) ;=> 7 notes, aeolian
; (:mkscale 60 'm) ;=> 7 notes, aeolian
; (:mkscale 60 'aeolian 5) ;=> 5 notes
; (:mkscale 60 'aeolian 0 3) ;=> 3 octaves (notes arg is ignored)
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(define :mkscale
  (lambda (root . args)
  ; (println args)
   (cond ((null? args) ;; default
          (add root (pc:scale 0 'ionian)))   
          ;
          ((length-equal? args 1) 
           ;; 1 arg = scale symbol only  
           (add root (pc:scale 0 (car args))))
          ((length-equal? args 2) 
            ;; 2 args: scale, num-notes 
            ;; factor: calc how big the scale before slicing. 5 is the min lenght of a scale. We double that based on on the num-notes parameter
            (let* ((factor (+ (real->integer (/ (cadr args) 5)) 1)) 
                   (s1 (helper:multiply_octaves (pc:scale 0 (car args)) factor)))
              (slice (cadr args) (add root s1))))
          ;
          ((length-equal? args 3) 
            ;; 3 args: scale, num-notes (ignored), octaves 
            ;; added for backward compatibility
            (add root (helper:multiply_octaves (pc:scale 0 (car args)) (caddr args))))
          ;
          (else (print 'Error: 'arguments 'could 'not 'be 'resolved.))              
      )
    )          
)            





;
;
; mkrelscale
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Makes a scale and allows passing an pitch value to transpose it
; The pitch value is used to get a new note from the pitch class.
; IE combines mkscale and pc:relative
;
; Args:
; root    [eg: 60]
; reldegree    [eg: int, positive or negative]
; mode    [eg: 'ionian]
; num-notes [eg: 1]
;
;
; Example:
; (mkscale 60 'ionian 3)     ;; '(60 62 64)
; (mkrelscale 60 0 'ionian 3) ;; '(60 62 64)
; (mkrelscale 60 2 'ionian 3) ;; '(64 66 68) => take 2nd pitch from ionian pitch class, get note and start new scale from that note
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(define :mkrelscale
  (lambda (root reldegree mode num-notes)
    (:mkscale (pc:relative root reldegree (pc:scale 0 mode)) mode num-notes)))









(define *maxpitch* 110)
(define *minpitch* 10)

; Intervals are abbreviated with m for minor, M for major, d for diminished, A for augmented, P for perfect.
;; current implementation only has major and minor (perfect is implicit)
;; TODO d for diminished, A for augmented
(define *interval-quality* (list 'M 'm)) 


;; turn a pitch class step interval notation to a more traditional interval notation
;; eg 
;; intervallo di 1 = same note VS 0 in pc steps
;; intervallo di 2 = next pitch in scale VS 1 in pc steps
;; intervallo di 4 = quarta eg 'fa' in do maggiore VS 3 in pc steps
;; intervallo di 8 = ottava VS 7 in pc steps
;
(define helper:pc_to_interval
  (lambda (pcstep)
    (cond	((> pcstep 0)
            (- pcstep 1))
          ((< pcstep 0)
            (+ pcstep 1))
          (else 0))
    )
)  




; https://en.wikipedia.org/wiki/Interval_(music)#Interval_number_and_quality
(define helper:calc-quality-interval
  (lambda (root number quality)
    (cond	((or (= number 0) (= number 1))
            root) 
          ((and (= number 2) (equal? quality 'm))
            (add 1 root)) 
          ((and (= number 2) (equal? quality 'M))
            (add 2 root)) 
          ((and (= number 3) (equal? quality 'm))
            (add 3 root)) 
          ((and (= number 3) (equal? quality 'M))
            (add 4 root)) 
          ((= number 4) ;; always perfect / ignore quality value
            (add 5 root)) 
          ((= number 5) ;; always perfect / ignore quality value
            (add 7 root)) 
          ((and (= number 6) (equal? quality 'm))
            (add 8 root)) 
          ((and (= number 6) (equal? quality 'M))
            (add 9 root)) 
          ((and (= number 7) (equal? quality 'm))
            (add 10 root)) 
          ((and (= number 7) (equal? quality 'M))
            (add 11 root)) 
          ((= number 8) ;; always perfect / ignore quality value
            (add 12 root)) 
          ((and (= number 9) (equal? quality 'm))
            (add 13 root)) 
          ((and (= number 9) (equal? quality 'M))
            (add 14 root)) 
          ((and (= number 10) (equal? quality 'm))
            (add 15 root)) 
          ((and (= number 10) (equal? quality 'M))
            (add 16 root)) 
          (else root)) 
    ))


;
; helper for :mkint    
; calculate the interval using pc classes
; if results is not within the max/min range, then return the range boundaries (quantized for the requested scale!)
;
(define helper:mkint-inner
  (lambda (root degree scale_or_quality)
    (if (member scale_or_quality *interval-quality*)
        
        ; QUALITY interval
        (let ((out (helper:calc-quality-interval root degree scale_or_quality)))    
          (if (or (> out *maxpitch*) (< out *minpitch*)) 
              (set! out root))  
              out)    
         
        ; SCALE interval
        (let ((out (pc:relative root (helper:pc_to_interval degree) (pc:scale 0 scale_or_quality))))  
          (if (> out *maxpitch*)
            (set! out (pc:relative *maxpitch* 0 (pc:scale 0 scale_or_quality)))  
            (if (< out *minpitch*)
              (set! out  (pc:relative *minpitch* 0 (pc:scale 0 scale_or_quality)))))  
              out)  
        )
    )
          
)




;
;
; :mkint
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Returns a new pitch at the desired interval 
; To determine the interval quality, accepts either a scale/mode (defaults major/ionian) or a M/m symbol.  
;
; WHEN PASSING A SCALE (MODE)
; The interval is also *quantised* using that scale
;
; WHEN PASSING AN INTERVAL QUALITY (eg M/m/A/D)
; The interval gets calculated using the semitones count only
;
; NOTE interval numbers use the traditional notation (differently from the numbers used in pc:relative)
; That is: 
;; intervallo di 1 = unison (VS 0 in pc steps)
;; intervallo di 2 = next pitch in scale (VS 1 in pc steps)
;; intervallo di 4 = quarta eg 'fa' in do maggiore (VS 3 in pc steps)
;; intervallo di 8 = ottava (VS 7 in pc steps)
;
; ===Examples with Scales===
; (:mkint 60 0) => 60  (default scale: ionian)
; (:mkint 60 1) => 60
; (:mkint 60 2) => 62
; (:mkint 60 -1) => 60
; (:mkint 60 -2) => 59
; (:mkint 60 3) => 64
; (:mkint 60 3 'aeolian) => 63
; (:mkint 61 2) => 62 / major second in c (with quantization) 
; (:mkint 110 20) => 110  ;; maxlimit kicks in
; (:mkint 30 -40) => 10   ;; minlimit kicks in
;
;
; ===Examples with Qualities eg M/m===
; (:mkint 60 2 'M)  => 62
; (:mkint 60 3 'm)  => 63
; (:mkint 102 5 'M)  => 109
; (:mkint 105 5 'M)  => 105 ;; maxlimit kicks in
(define :mkint
  (lambda (root degree . args)
  ; (println args)
   (cond ((null? args) 
        ; 0 args: return a 'major' interval using the 'ionian mode
        (helper:mkint-inner root degree 'ionian))     
        ((length-equal? args 1) 
        ;; 1 arg: use whatever scale-mode symbol is passed
        (helper:mkint-inner root degree (car args)))
      ;
      (else (print 'Error: 'arguments 'could 'not 'be 'resolved.))               
      )
    )          
) 









;
;
; helper:mkmelody:pcrel
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; helper function: generate a random melody using pc:rel, a 
; root note and a list of relative steps variations
;
;
; Example:
; (helper:mkmelody:pcrel 4 60 'm '(-1)) 
; => (60 58 56 55)
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(define helper:mkmelody:pcrel
  (lambda (len startp scale stepslst)
      (let loop ((i 0)
                  (lst (list startp)))  
        ; (println lst)
        (if (< i (- len 1))
          (loop (+ i 1)
            (cons (pc:relative (car lst)
              (random stepslst)
              (pc:scale 0 scale))
            lst))   
          (reverse lst))))) 




;
;
; mkmelody
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Generates a random melody
;
; Args:
; number-of-notes 
; root [default: 60|c4]
; scale-mode [default: 'M]
; steps-variation [default: '(1 -1)]
;
; Example:
; (println (:mkmelody 4 60 'm '(-1) )) ; => (60 58 56 55)
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(define :mkmelody
  (lambda (len . args)
  ; (println args)
   (cond ((null? args) ;; default / number-of-notes
          (helper:mkmelody:pcrel len c4 'M '(1 -1)))    
          ;
          ((length-equal? args 1) 
           ;; 1 arg = root
           (helper:mkmelody:pcrel len (car args) 'M '(1 -1))) 
          ((length-equal? args 2) 
            ;; 2 args: root AND scale-mode
            (helper:mkmelody:pcrel len (car args) (cadr args) '(1 -1) )) 
          ;
          ((length-equal? args 3) 
            ;; 3 args: root AND scale-mode AND variations-list
            (helper:mkmelody:pcrel len (car args) (cadr args) (caddr args) )) 
          ;
          (else (print 'Error: 'arguments 'could 'not 'be 'resolved.))              
      )
    )          
)        






;
;
; mkrel // DEPRECATED? 2022-01-30
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Shortcut for pc:relative / returns a single note
;
; Args:
; root    [eg: 60]
; reldegree    [eg: int, positive or negative]
; mode    [eg: 'ionian]
;
;
; Example:
; (mkrel 60 0 'pentatonic) ;; 60
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(define :mkrel
  (lambda (root degree mode)
        (car (mkrelscale root degree mode 1))))



; octave, sixth etc..
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Shortcuts for incrementing pitch using intervals
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;
;

(define-macro (seventhdom plist)
 `(add 10 ,plist))
(define-macro (seventhmaj plist)
 `(add 11 ,plist))
(define-macro (sixth plist)
 `(add 9 ,plist))
(define-macro (fourth plist)
  `(add 5 ,plist))
(define-macro (fifth plist)
   `(add 7 ,plist))
(define-macro (thirdmaj plist)
  `(add 4 ,plist))
(define-macro (thirdmin plist)
  `(add 3 ,plist))




;
;
; :mkrandom
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; wrapper around pc:random 
;;
;; Allows passing a scale symbol, or any list of pitches.
;; With symbol, it defauls to C as root.
;; (original only allows a 0-11 pitch class list) 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(define :mkrandom
    (lambda (lower upper pitch-list-or-sym)
      (let ((scale pitch-list-or-sym))
        (if	(atom? scale)
            (set! scale (:mkscale 60 pitch-list-or-sym)))
        (pc:random lower upper 
            (map (lambda (x)
                    (modulo x 12))
                scale)
            ))))





;
;
; :mkquantize
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; wrapper around pc:quantize
;;
;; Allows passing a scale symbol, or any list of pitches.
;; With symbol, it defauls to C as root. 
;; (original only allows a 0-11 pitch class list) 
;;
;; Returns always the closest value
;;  (:mkquantize 60 'M) => 60
;;  (:mkquantize 60 (list 1 11)) => 61
;;  (:mkquantize 60 (list 63 59)) => 59
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(define :mkquantize
    (lambda (pitch-in pitch-list-or-sym)
      (let ((scale pitch-list-or-sym))
        (if	(atom? scale)
            (set! scale (:mkscale 60 pitch-list-or-sym)))
        (pc:quantize pitch-in 
            (map (lambda (x)
                    (modulo x 12))
                scale)      
            ))))








;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Helper method for 'octave' 
;; example
;; (in-octave 60 1 3) ; +> 12
(define to-octave 
  (lambda (pitch min max)
    (if (> (+ min max) 16) 
        (log-info "Octaves number should be between 1 and 8. 5=60/c4") 
        (let ((octavemin (* min 12)) 
              (octavemax (* max 12)))   
          (if (or (< pitch octavemin) (> pitch octavemax)) 
            (add (pc:pc pitch) octavemin)  
            pitch))))) 
    



;; OCTAVE 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Move a pitch to a different octave range, within with min/max boundaries.
;
;; arg1: pitch or pitch-list
;; arg2: min boundary 
;; arg3: max boundary (if not provided, min +1)
;
;;  Octave 1 = c0 = 12  // 1st octave starts
;;  Octave 2 = c1 = 24  
;;  Octave 3 = c2 = 36  // 3rd octave starts
;;  Octave 4 = c3 = 48
;;  Octave 5 = c4 = 60  // 5th octave starts
;;  Octave 6 = c5 = 72
;;  Octave 7 = c6 = 84  // 7th octave starts
;;  Octave 8 = c7 = 96
;
;; Examples
; (octave 10) ; +> 22 / simply add 12 semitones
; (octave '(10 20)) ; +> (22 32) / add 12 semitones to a list
; (octave 60 3) ; 36 / move to 3rd octave
; (octave 60 3 6) ; 60 / move to octave between 3rd and 6th
; (octave '(60 72) 3 6) ; (60 72) / move list to octave between 3rd and 6th 
;;
(define octave
  (lambda (plist . args)
  ; (println args)
   (cond ((null? args) ;; default - intervallo di ottava
          (add 12 plist))   
          ((length-equal? args 1) 
           ;; 1 arg = min boundary only / max boundary inferred 
           (if (list? plist) 
                  (map (lambda (x)
                      (to-octave x (car args) (+ (car args) 1)))  
                    plist) 
                (to-octave plist (car args) (+ (car args) 1))) 
           )  
            
          ((length-equal? args 2) 
            ;; 2 args: min boundary, max boundary
           (if (list? plist) 
                  (map (lambda (x)
                      (to-octave x (car args) (cadr args)))  
                    plist) 
                (to-octave plist (car args) (cadr args))) 
           ) 
          ;
          (else (print 'Error: 'arguments 'could 'not 'be 'resolved.))              
      )
    )          
)   


